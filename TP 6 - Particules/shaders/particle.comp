#version 430

// Taille du workgroup
layout(local_size_x = 256, local_size_y = 1, local_size_z = 1) in;

// Structure de particule identique à celle du CPU
struct Particle {
    vec3 pos;      
    float age;     
    vec3 speed;    
    float ageMax;  
    int type;
    float padding[3];
};

// Storage Buffer Object contenant les particules
layout(std430, binding = 0) buffer ParticleBuffer {
    Particle particles[];
};

// Uniforms
uniform float deltaTime;
uniform float gravity = 5.0;

// Fonction d'initialisation d'une particule
void initParticle(uint index) {
uint seed = index * 747796405u + 2891336453u;
    
    particles[index].pos = vec3(0.0, 0.0, 0.0);
    
    // Type aléatoire
    uint random = seed % 100u;
    if (random < 40u) {
        particles[index].type = 0;  // Feu
    } else if (random < 90u) {
        particles[index].type = 1;  // Fumée
    } else {
        particles[index].type = 2;  // Etincelles
    }
    seed = seed * 747796405u + 2891336453u;
    
    float angle = float(seed) / 4294967295.0 * 2.0 * 3.14159265;
    seed = seed * 747796405u + 2891336453u;
    
    float norm = 0.04 * float(seed) / 4294967295.0;
    seed = seed * 747796405u + 2891336453u;
    
    // Comportements selon le type
    if (particles[index].type == 0) { // Feu
        particles[index].speed.x = norm * cos(angle);
        particles[index].speed.y = norm * sin(angle);
        particles[index].speed.z = 1.5 + 0.5 * float(seed) / 4294967295.0;
        particles[index].ageMax = 3.0 + 3.0 * float(seed) / 4294967295.0;
    } else if (particles[index].type == 1) { // Fumée
        particles[index].speed.x = norm * 2.0 * cos(angle);
        particles[index].speed.y = norm * 2.0 * sin(angle);
        particles[index].speed.z = 0.3 + 0.3 * float(seed) / 4294967295.0;
        particles[index].ageMax = 8.0 + 5.0 * float(seed) / 4294967295.0;
    } else { // Etincelles
        particles[index].speed.x = norm * 5.0 * cos(angle);
        particles[index].speed.y = norm * 5.0 * sin(angle);
        particles[index].speed.z = 2.0 + 1.0 * float(seed) / 4294967295.0;
        particles[index].ageMax = 2.0 + 2.0 * float(seed) / 4294967295.0;
    }
    
    particles[index].age = 0.0;
}

void main() {
    uint index = gl_GlobalInvocationID.x;
    if (index >= particles.length()) return;
    
    float localGravity = gravity;
    
    // Gravité différente selon le type
    if (particles[index].type == 1) { // Fumée : gravité inversée
        localGravity = -1.0;
    } else if (particles[index].type == 2) { // Etincelles : gravité normale
        localGravity = gravity;
    }
    
    particles[index].speed.z -= localGravity * deltaTime;
    particles[index].pos += particles[index].speed * deltaTime;
    
    if (particles[index].pos.z < 0.0) {
        particles[index].speed.z = -0.8 * particles[index].speed.z;
        particles[index].pos.z = 0.0;
    }
    
    particles[index].age += deltaTime;
    
    if (particles[index].age >= particles[index].ageMax) {
        initParticle(index);
    }
}
